%% file watercan.mcrl2

act

  empty, fill, done,a;
  tocan, fromcan, pour, go: Nat # Nat # Nat # Nat # Nat # Nat;

map 
  target,bound: Nat;
eqn 
  target = 2;
  bound = 10;

proc

  Can(n:Nat,m:Nat) =
    ( m != target ) -> (
      (m > 0) -> (empty . Can(n,0)) +
      (m < n) -> (fill . Can(n,n)) +
      sum l,x,y,z:Nat . ( 
        ((l <= n) && (y <= z) && (z <= x) && (x <= bound)) ->
          tocan(n,m,l,x,y,z) . Can(n,l) ) +
     sum x,y,z,l:Nat . ( 
        ((l <= n) && (y <= x) && (z <= x) && (x <= bound)) ->
          fromcan(x,y,z,n,m,l) . Can(n,l) )
    ) <> done ; %% .Can(n,m);
    %% with looping on Can(n,m) everything is OK
    %% for termination, weird things happen to the lps, e.g. new states are
    %% introduced and only one proper fill-transition is missing

  Pour =
    sum n,m,l,x,y,z:Nat .
      (m > 0 && m <= n && l <= n && n <= bound && y <= x && z <= x && x <= bound) -> (
        (m <= x-y) -> (
          pour(n,m,0,x,y,Int2Nat(y+m)) . Pour 
        ) <> (
          pour(n,m,Int2Nat(m-x+y),x,y,x) . Pour
        ));

init

  allow( 
    { go, empty, fill,a },
  comm(
    { tocan|fromcan|pour -> go },
  Can(4,0) || Can(3,0) || Pour
    ));

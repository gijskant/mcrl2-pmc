% Workers (block-specification)
%
% This is an artificial constructed specification of workers, where each worker
% has a specific task. Every worker has a unique ID, which other workers can
% address. If a worker needs to address another worker, it selects the worker
% with the lowest ID value, that is not occupied. When a worker has finished 
% the job it sends a notification, that is has finished the task.
% There are three kind of workers:
% * A hard worker: Immediately executes an addressed task. (S)
% * A messenger:   Takes a task and delivers it to another worker. A messenger 
%                  cannot accept a new task until the delegated task has been
%                  completed. (P)
% * A lazy worker: Takes a task and delivers it simultaneously to two other 
%                  workers. A lazy worker cannot accept a new task, when it has
%                  delegated a task. The lazy worker only accepts the 
%                  notification of delegated work. (M1)
% Only hard workers require resources. This specification has unlimited 
% resources for hard workers.
%
% Created by Frank Stappers, 
% 16 June 2009

act s,s',d,d': Nat;
  
proc P(n: Nat) = s(n).s(n+1).d(n+1).d(n).P(n);

proc M1(n: Nat) = s(n). s(n+1)|s(n+2). d(n+1)|d(n+2).d(n).M1(n);
  
proc S(n: Nat) = s(n).d(n).S(n);
  
init
  block({s,d},
    comm( { s | s -> s'
          , d | d -> d'
          }                                                                                                                           
          , P(0) || M1(1) || S(2) || P(3) || S(4) || (s(0).d(0))
        )
  )
;


